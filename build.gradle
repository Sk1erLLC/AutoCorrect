buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = '1.8.9-11.15.1.1722'
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation ("org.languagetool:language-en:4.6") {
        exclude module: "asm"
        exclude module: "guava"
    }
    implementation group: 'javax.measure', name: 'jsr-275', version: '0.9.1'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes(
                "FMLCorePlugin": "club.sk1er.autocorrect.tweaker.AutoCorrectTweaker",
                "ForceLoadAsMod": true, FMLCorePluginContainsFMLMod: true,
                "ModSide": "CLIENT",
                'FMLAT': 'autocorrect_at.cfg'
        )
    }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

shadowJar {
    minimize {
        exclude(dependency("org.languagetool:.*:.*"))
        exclude(dependency("net.loomchild:segment:.*"))
        exclude(dependency("org.apache.commons:.*:.*"))
        exclude(dependency("org.apache:.*:.*"))
        exclude(dependency("javax.measure:.*:.*"))
        exclude(dependency("tech.units:.*:.*"))
        exclude(dependency("tech.uom.lib:.*:.*"))
        exclude(dependency("ml.dmlc:.*:.*"))
        exclude(dependency("org.slf4j:.*:.*"))
        exclude(dependency("org.carrot2:.*:.*"))
        exclude(dependency("org.apache.opennlp:.*:.*"))
        exclude(dependency("com.carrotsearch:.*:.*"))
    }

    relocate 'org.apache.commons.csv', 'club.sk1er.org.apache.commons.csv'
    relocate 'org.apache.commons.text', 'club.sk1er.org.apache.commons.text'
    relocate 'org.apache.commons.lang3', 'club.sk1er.org.apache.commons.lang3'
    relocate 'org.apache.commons.pool2', 'club.sk1er.org.apache.commons.pool2'

    exclude 'module-info.class'
    exclude 'META-INF/versions/**'
    exclude '**/*.png'
    exclude '**/*.jpg'
    exclude '**/*.gif'
    exclude '**/*.js'
    exclude '**/*.html'
    exclude '**/*.css'
//    exclude '**/*.bin'

    exclude 'lib/**'
    exclude 'com/sun/jna/**'

    // These look like dev dependencies
    exclude 'languages.shorttext/**'
    exclude 'languages/**'

    classifier ''
}